import glob
import pandas as pd
import snakemake.utils

# User-specified dataset-specific config file
dataset_config_file = config.get("dataset_config_file", None)

# Load fixed config parameters
configfile: "config/params.yaml"

# Check if the dataset-specific config file is provided
if dataset_config_file is not None:
    # Load dataset-specific parameters
    configfile: dataset_config_file

include: 'rules/prepare.smk'
include: 'rules/structure.smk'
include: 'rules/sequence.smk'
include: 'rules/checks.smk'
include: 'rules/common.smk'
include: 'rules/analysis.smk'

input_table = pd.read_csv(config['input_table_file'], header=None, sep='\t')
input_table.columns = ['uniprot', 'taxid', 'count1', 'count2', 'count3', 'genome', 'source', 'species', 'mnemo']
input_dict = input_table.set_index('uniprot').T.to_dict()

codes = list(input_table['uniprot'])

outdir=config['outdir']+config['dataset']

# possible combinations:
combinations_codes_str=[x+"_"+y for x in codes for y in codes]

# mattypes = ['fident', 'alntmscore', 'lddt']
modes = ['blast', 'fs', 'common', 'union']
methods = ['blast', 'fs']

alphabets = ['aa', '3Di']
alphabets_fident = ['aa', '3Di', 'fident']

models=["3Di", "QT", "LG", "GTR", "FT"]
combinations=["3Di_3Di", "aa_QT", "aa_LG", "3Di_GTR", "3Di_FT", "3Di_FTPY"]
combinations_ML=["3Di_3Di", "aa_LG", "3Di_GTR"]

plots = ["homology", "saturation", "runtime", "astral_pro", "trees", "discordance", "singletons", "distance", "examples"]

rule all:
    input:
        expand(outdir+"/homology/{seed}_blast_brh.tsv", seed=config['seed']),
        expand(outdir+"/homology/{seed}_fs_brh.tsv", seed=config['seed']),
        outdir+'/db/db_num.tsv',
        expand(outdir+"/plots/{seed}_{plot}.pdf", seed=config['seed'], plot=plots),
        expand(outdir+"/plots/{seed}_foldmason", seed=config['seed']),
        expand(config['cath_dir']+'{code}_cath.tsv', code=codes)
